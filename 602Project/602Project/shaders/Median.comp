#version 460

/*
3x3 Median
Morgan McGuire and Kyle Whitson
http://graphics.cs.williams.edu

Modified by Krishna Pillai - krishna_sp@outlook.com
https://github.com/anhsirkrishna

Copyright (c) Morgan McGuire and Williams College, 2006
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are
met:

Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in the
documentation and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "shared_structs.h"

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba32f) uniform image2D in_buffer_bg;
layout(set = 0, binding = 1, rgba32f) uniform image2D in_buffer_fg;
layout(set = 0, binding = 2, rgba32f) uniform image2D out_buffer_bg;
layout(set = 0, binding = 3, rgba32f) uniform image2D out_buffer_fg;
layout(set = 0, binding = 4, rgba32f) uniform image2D in_buffer_rt;
layout(set = 0, binding = 5, rgba32f) uniform image2D out_buffer_rt;

#define s2(a, b)				temp = a; a = min(a, b); b = max(temp, b);
#define mn3(a, b, c)			s2(a, b); s2(a, c);
#define mx3(a, b, c)			s2(b, c); s2(a, c);

#define mnmx3(a, b, c)			mx3(a, b, c); s2(a, b);                                   // 3 exchanges
#define mnmx4(a, b, c, d)		s2(a, b); s2(c, d); s2(a, c); s2(b, d);                   // 4 exchanges
#define mnmx5(a, b, c, d, e)	s2(a, b); s2(c, d); mn3(a, c, e); mx3(b, d, e);           // 6 exchanges
#define mnmx6(a, b, c, d, e, f) s2(a, d); s2(b, e); s2(c, f); mn3(a, b, c); mx3(d, e, f); // 7 exchanges

void main()
{
    ivec2 gpos = ivec2(gl_GlobalInvocationID.xy);
   
    vec4 v_bg[9];
    vec4 v_fg[9];
    vec4 v_rt[9];

    // Add the pixels which make up our window to the pixel array.
    for(int dX = -1; dX <= 1; ++dX) {
        for(int dY = -1; dY <= 1; ++dY) {		
            ivec2 offset = ivec2(dX, dY);
		    
            // If a pixel in the window is located at (x+dX, y+dY), put it at index (dX + R)(2R + 1) + (dY + R) of the
            // pixel array. This will fill the pixel array, with the top left pixel of the window at pixel[0] and the
            // bottom right pixel of the window at pixel[N-1].
            v_bg[(dX + 1) * 3 + (dY + 1)] = imageLoad(in_buffer_bg, gpos + offset);
            v_fg[(dX + 1) * 3 + (dY + 1)] = imageLoad(in_buffer_fg, gpos + offset);
            v_rt[(dX + 1) * 3 + (dY + 1)] = imageLoad(in_buffer_rt, gpos + offset);
        }
    }
    vec4 temp;

    // Starting with a subset of size 6, remove the min and max each time
    mnmx6(v_bg[0], v_bg[1], v_bg[2], v_bg[3], v_bg[4], v_bg[5]);
    mnmx5(v_bg[1], v_bg[2], v_bg[3], v_bg[4], v_bg[6]);
    mnmx4(v_bg[2], v_bg[3], v_bg[4], v_bg[7]);
    mnmx3(v_bg[3], v_bg[4], v_bg[8]);

    imageStore(out_buffer_bg, gpos, v_bg[4]);

    // Starting with a subset of size 6, remove the min and max each time
    mnmx6(v_fg[0], v_fg[1], v_fg[2], v_fg[3], v_fg[4], v_fg[5]);
    mnmx5(v_fg[1], v_fg[2], v_fg[3], v_fg[4], v_fg[6]);
    mnmx4(v_fg[2], v_fg[3], v_fg[4], v_fg[7]);
    mnmx3(v_fg[3], v_fg[4], v_fg[8]);

    imageStore(out_buffer_fg, gpos, v_fg[4]);

    // Starting with a subset of size 6, remove the min and max each time
    mnmx6(v_rt[0], v_rt[1], v_rt[2], v_rt[3], v_rt[4], v_rt[5]);
    mnmx5(v_rt[1], v_rt[2], v_rt[3], v_rt[4], v_rt[6]);
    mnmx4(v_rt[2], v_rt[3], v_rt[4], v_rt[7]);
    mnmx3(v_rt[3], v_rt[4], v_rt[8]);

    imageStore(out_buffer_rt, gpos, v_rt[4]);
}
